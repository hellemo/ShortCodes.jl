module QRCodersExt

using LinearAlgebra
using QRCoders
using ShortCodes

function _to_ascii(A, TRUE="██", FALSE="  ")
    join((join( b ? TRUE : FALSE for b in r ) for r in eachrow(A)),"\n")
end

function Base.show(io::IO, ::MIME"image/png", q::ShortCodes.QRC)
    bitmap = .!qrcode(q.payload)
    N = Int(round(q.size / size(bitmap, 1)))
    bitmap = kron(bitmap, ones(Bool, N, N))
    QRCoders.FileIO.save(QRCoders.FileIO.Stream(QRCoders.FileIO.format"PNG", io), bitmap)
end

function Base.show(io::IO, ::MIME"text/plain", q::ShortCodes.QRC)
    print(io, _to_ascii(qrcode(q.payload)))
end

function Base.show(io::IO, ::MIME"image/svg+xml", q::ShortCodes.QRC)
    write(io, bitmatrix_to_svg(qrcode(q.payload)))
end

"""
    bitmatrix_to_svg(bitmatrix::BitMatrix; module_size::Int=10, margin::Int=4) -> String

Generate an SVG image string representing a QR code from a `BitMatrix`.

# Arguments
- `bitmatrix::BitMatrix`: A matrix of bits where `true` represents a black module and `false` a white one.
- `module_size::Int=10`: The size (in pixels) of each QR code module (square).
- `margin::Int=4`: The number of white modules to include as margin around the QR code.

# Returns
- `String`: An SVG-formatted string representing the QR code.

# Example
```julia
using QRCoders

matrix = qrcode("https://example.com")
svg_str = bitmatrix_to_svg(matrix)
write("qrcode.svg", svg_str)

This function was generated by Copilot
"""
function bitmatrix_to_svg(bitmatrix::BitMatrix; module_size::Int=10, margin::Int=4)
    rows, cols = size(bitmatrix)
    width = (cols + 2 * margin) * module_size
    height = (rows + 2 * margin) * module_size

    svg = IOBuffer()
    println(svg, """<svg xmlns="http://www.w3.org/2000/svg" width="$width" height="$height" viewBox="0 0 $width $height">""")
    println(svg, """<rect width="100%" height="100%" fill="white"/>""")

    for y in 1:rows
        for x in 1:cols
            if bitmatrix[y, x]
                xpos = (x - 1 + margin) * module_size
                ypos = (y - 1 + margin) * module_size
                println(svg, """<rect x="$xpos" y="$ypos" width="$module_size" height="$module_size" fill="black"/>""")
            end
        end
    end

    println(svg, "</svg>")
    return String(take!(svg))
end


end